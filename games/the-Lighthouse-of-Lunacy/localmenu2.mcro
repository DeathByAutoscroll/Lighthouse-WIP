### Localmenu2 ###
### By Armystuntman AKA Jupiter ###
### COMMANDS ###
NOTE: To change the main.png backgrounds in your entire case, use:

set _localmenu_bg image
set _bigbutton_bg bg/image
set _list_bg_image bg/main

#Part 1: Localmenu2
Localmenu2 follows a specific syntax.
1. Set up the background/character ect.
2. Give your localmenu a label name, and define which options are available by setting to true or false. (Setting all to false skips the line.)
3. Then add "goto $_localmenu" after it. (REQUIRED!)
4. Create labels below it that follow syntax "-labelname-_talk" "-labelname-_examine" ect as you need.
5. "goto $_localmenu_back" goes back to the main localmenu2, while "goto $_localmenu" goes to the currently selected sub-menu. (Talk, examine ect)

### Example: Your label is called "mia_menu1" and the bg is "miaoffice". ###
label mia_menu1
localmenu2 mia_menu1 examine=true move=true talk=true present=true
goto $_localmenu

### EXAMINE MENU EXAMPLE ###
label mia_menu1_examine
examine_scrollon miaoffice
examine fail=mia_menu1_generic
region 0 0 256 192 mia_menu1_generic
examine_scrolloff
goto $_localmenu_back

label mia_menu1_generic
examine_scrolloff
char Phoenix hide
"This sure is an office!"
goto $_localmenu

### PRESENT MENU EXAMPLE ###
label mia_menu1_present
examine_scrollon miaoffice
examine fail=mia_menu1_generic
region 0 0 256 192 mia_menu1_generic
examine_scrolloff
goto $_localmenu_back

label mia_menu1_generic
examine_scrolloff
char Phoenix hide
"This sure is an office!"
goto $_localmenu

### TALK MENU EXAMPLE ###
list mia_menu1_talk ###List is also a lable, if you want to save a line.
li Topic result=mia_menu1_topic_1
showlist
goto $_localmenu_back

label mia_menu1_topic_1
char Phoenix hide
"Hello Mia."
char Mia e=normal
"Hello Nick."
goto $_localmenu

### MOVE MENU EXAMPLE ###

list mia_menu1_move ###List names count as lables, if you don't want to write a label before it.
li Court result=goto_court
showlist
goto $_localmenu_back

label goto_court
script your_next_script_here


### END OF EXAMPLE ###

### Localmenu2 Override variables. ###
Localmenu2 accepts variables that all start with the syntax "set -labelmenu-_talk true/false"
For example, if you have:

	localmenu2 mia_menu1 examine=true move=true

And you do:

	set mia_menu1_examine false

Then the move button will be disabled, even though the localmenu2 line has =true.
This works for "examine" "move" "present" "talk".

###############################

### DOUBLE SCREEN LOCALMENU ###
Double Backgrounds act as two different localmenus. This script only handles scrolling between them. Sadly, does not work in Examines because PyWright examine scrolling only works in one direction, so don't even bother.

### EXAMPLE ####
label the_menu_left
localmenu2 the_menu_left examine=true move=true menuscroll=the_menu_right scrollx=-256 scrolly=0 scroll=r
goto $_localmenu

label the_menu_right
localmenu2 the_menu_right examine=true move=true menuscroll=the_menu_left scrollx=256 scrolly=0 scroll=l
goto $_localmenu

### END OF EXAMPLE ###

###Character Emotes###
#Because char replaces characters, use setchar and setemo for easier conversations if you have multiple characters stacked on screen
Doesn't work for hidden characters, use "char [character] hide" for that.

	setchar name emotion
	setemo name emotion
###############################















###############################
### ACTUAL MACROS HERE ###

macro fadechar
set _fadechar_internal $1
set _fadechar_name_internal $name
isnotempty _fadechar_name_internal?
getvar _speaking _fadechar_name_internal

is _fadechar_internal out?
fade name=$_speaking start=100 end=0 speed=6 nowait
is _fadechar_internal in?
fade name=$_speaking start=0 end=100 speed=6 nowait
pause 30 script
endmacro

macro setchar
set _setchar_emo_internal $2
isnotempty _setchar_emo_internal?
emo $2 name=$1
set _speaking $1
endmacro

macro setemo
emo $2 name=$1
endmacro

#localmenuscroll x=0 y=0
#Included in localmenu2 automatically if scrolldir is defined.
macro localmenuscroll
set scroll_internal $scrolldir
delete name=examine_scroll_button suppress
delete name=examine_scroll_button suppress
set internal_scroll_distancex $x
set internal_scroll_distancey $y
set menuscroll_name $menuscroll
is scroll_internal r?
gui Button {scroll_localmenu_button} x=102 y=360 z=7 name=examine_scroll_button graphic=general/examine_scroll_r graphichigh=general/examine_scroll_r_high try_bottom
is scroll_internal l?
gui Button {scroll_localmenu_button} x=102 y=360 z=7 name=examine_scroll_button graphic=general/examine_scroll_l graphichigh=general/examine_scroll_l_high try_bottom
endmacro
#When scrolling is activated.
macro scroll_localmenu_button
gui_flush
gui Button {invalid} name=examine_scroll_button graphic=general/examine_scroll_none x=102 y=360 z=5 stack
sfx Scroll
delflag localmenu_open
set _localmenu $menuscroll_name

is scroll_internal l scroll_left
scroll x=$internal_scroll_distancex y=$internal_scroll_distancey speed=6 nowait
pause 45 script
setflag screen_right
companionout
goto endmacro

label scroll_left
scroll x=$internal_scroll_distancex y=$internal_scroll_distancey speed=6 nowait
pause 45 script
delflag screen_right
companionout
goto endmacro

label endmacro

endmacro


#The new localmenu! Example:
#label menulabel
#localmenu2 menulabel examine=true move=true
#goto $_localmenu

#menulabel_examine
#EXAMINE CODE
#goto $_localmenu_back


macro localmenu2

flag localmenu_open skiplocalmenu

setflag localmenu_open

gui_flush
set _localmenu $1
set _localmenu_back $1
set _localmenu_name $1

#Check variables.
getvar _examinecheck $_localmenu_back _examine
getvar _movecheck $_localmenu_back _move
getvar _talkcheck $_localmenu_back _talk
getvar _presentcheck $_localmenu_back _present
getvar _companionmenucheck $_localmenu_back _companionmenu

#SET DEFAULTS
isempty _examinecheck?
set _examinecheck $examine
isempty _movecheck?
set _movecheck $move
isempty _talkcheck?
set _talkcheck $talk
isempty _presentcheck?
set _presentcheck $present
isempty _companionmenucheck?
set _companionmenucheck $companionmenu
#reset defaults.
set examine
set move
set talk
set present
set companionmenu

#Buttons
is _examinecheck true?
gui Button {examine_localmenu_button} x=16 y=260 z=6 name=examinebutton graphic=general/localmenu/examine graphichigh=general/localmenu/examine_high try_bottom
is _movecheck true?
gui Button {move_localmenu_button} x=130 y=260 z=6 name=movebutton graphic=general/localmenu/move graphichigh=general/localmenu/move_high try_bottom
is _talkcheck true?
gui Button {talk_localmenu_button} x=16 y=290 z=6 name=talkbutton graphic=general/localmenu/talk graphichigh=general/localmenu/talk_high try_bottom
is _presentcheck true?
gui Button {present_localmenu_button} x=130 y=290 z=6 name=presentbutton graphic=general/localmenu/present graphichigh=general/localmenu/present_high try_bottom

noflag localmenu2_nopress skip_talk_present_buttons
#Nopress buttons.
isnot _examinecheck true?
gui Button {invalid} x=16 y=260 z=6 name=examinebutton graphic=general/localmenu/examine_nopress try_bottom
isnot _movecheck true?
gui Button {invalid} x=130 y=260 z=6 name=movebutton graphic=general/localmenu/move_nopress try_bottom

isnot _talkcheck true?
isnot _presentcheck true skip_talk_present_buttons

isnot _talkcheck true?
gui Button {invalid} x=16 y=290 z=6 name=talkbutton graphic=general/localmenu/talk_nopress try_bottom
isnot _presentcheck true?
gui Button {invalid} x=130 y=290 z=6 name=presentbutton graphic=general/localmenu/present_nopress try_bottom
label skip_talk_present_buttons

noflag companion skipcompanionbuttons
is _companionmenucheck true?
gui Button {companion_localmenu_button} x=0 y=192 z=6 name=companionbutton graphic=general/localmenu/companion graphichigh=general/localmenu/companion_high try_bottom

isnot _companionmenucheck true?
gui Button {invalid} x=0 y=192 z=6 name=companionbutton graphic=general/localmenu/companion_nopress try_bottom

label skipcompanionbuttons

label skiplocalmenu

#isnotempty menuscroll?
localmenuscroll menuscroll=$menuscroll x=$scrollx y=$scrolly scrolldir=$scroll


show_court_record_button

label loop
pause 30 script
flag localmenu_open loop
endmacro

#sfx for invalid buttons.
macro invalid
sfx Blink
endmacro

#Pressing localmenu2 buttons.

macro present_localmenu_button
delete name=presentbutton suppress
gui_flush
sfx Confirm
joinvar _localmenu $_localmenu_back _present
delflag localmenu_open
endmacro

macro talk_localmenu_button
delete name=talkbutton suppress
gui_flush
sfx Confirm
joinvar _localmenu $_localmenu_back _talk
delflag localmenu_open
endmacro

macro move_localmenu_button
delete name=movebutton suppress
gui_flush
sfx Confirm
joinvar _localmenu $_localmenu_name _move
delflag localmenu_open
endmacro

macro examine_localmenu_button
delete name=examinebutton suppress
gui_flush
sfx Confirm
joinvar _localmenu $_localmenu_name _examine
delflag localmenu_open
endmacro

###########
EXAMINE:
#Examine scroll on.
macro examine_scrollon
set _examine_use false
flag examine_menu_open skip
gui_flush
set examine_scrollon_dark $dark
fg examine stack y=192 z=5
fade start=100 end=0 speed=10 name=guibg nowait

label skip_dark
is examine_scrollon_dark false skip_dark
fg black name=darkexamine z=6
fade start=0 end=40 speed=4 name=darkexamine nowait
label skip_dark

set scrollon_bg_x $x
set scrollon_bg_y $y
isempty scrollon_bg_x?
set scrollon_bg_x 0
isempty scrollon_bg_y?
set scrollon_bg_y 0
set scrollon_bg_is_fg $fg

disable_court_record_button
isnot scrollon_bg_is_fg true?
bg $1 stack name=backgroundexamine x=$scrollon_bg_x y=$scrollon_bg_y z=4 nowait
is scrollon_bg_is_fg true?
fg $1 stack name=backgroundexamine x=$scrollon_bg_x y=$scrollon_bg_y z=4 nowait
fade start=0 end=100 speed=6 name=backgroundexamine nowait
pause 10 script
sfx Scroll
set internal_scrollon_adjustment_fix $fix_bottom
isnot internal_scrollon_adjustment_fix true?
scroll name=backgroundexamine y=192 speed=20 nowait
is internal_scrollon_adjustment_fix true?
scroll name=backgroundexamine y=177 speed=20 nowait
setflag examine_menu_open
set examine_nocr $nocr
isnot examine_nocr true {enable_court_record_button}
label skip
endmacro

#Disable examine mode, MUST be run when out of examine.
macro examine_scrolloff
noflag examine_menu_open skip
delflag examine_menu_open
isnot examine_scrollon_dark false?
fade start=40 end=0 speed=6 name=darkexamine nowait
isnot internal_scrollon_adjustment_fix true?
scroll name=backgroundexamine y=-192 speed=20 nowait
is internal_scrollon_adjustment_fix true?
scroll name=backgroundexamine y=-177 speed=20 nowait
sfx Scroll
pause 10 script
fade start=100 end=0 speed=10 name=backgroundexamine nowait
fade start=0 end=100 speed=10 name=guibg nowait
pause 5 script
set _cr_button true
gui_flush
delete name=backgroundexamine suppress
delete name=darkexamine suppress
label skip
set _examine_use
endmacro

###GUI FLUSHING###
#fancy screen clearing, to remove specific elements seamlessly.

#Flush lower screen, re-add lower screen bg.
macro gui_flush
gui_flush_buttons
clear bottom
isempty _localmenu_bg?
set _localmenu_bg main2
bg $_localmenu_bg y=192 stack z=-5 name=guibg
delete name=penalty suppress
delete name=darkexamine suppress
endmacro

#Flush lower screen, do not re-add background.
macro gui_flush2
gui_flush_buttons
clear bottom
delete name=penalty suppress
delete name=darkexamine suppress
endmacro

#Flushes everything, re-adds bottom background. A fancy clear command.
#Also resets localmenu variables. This is basically an "end of scene" command.
macro gui_clear
clear
"{spd0}{sound/nothing}{next}"
delflag screen_right
delflag localmenu_open
endmacro

#Flushes bottom GUI, fades, then clears everything but re-adds gui bg.
macro gui_clear_fade
fg fadeout2 name=fadein_internal
pause 15 script
clear
"{spd0}{sound/nothing}{next}"
endmacro

#Flushes bottom GUI, fades + music fade, then clears everything but re-adds gui bg.
macro end_scene

fg fadeout2 name=fadein_internal nowait
music_fade_out_fast
clear
"{spd0}{sound/nothing}{next}"
endmacro


#Deletes custom GUI buttons, add your buttons to this list to simplify things (but try not to add too many!)
macro gui_flush_buttons
delete name=examine_scroll_button suppress
delete name=talkbutton suppress
delete name=movebutton suppress
delete name=examinebutton suppress
delete name=presentbutton suppress
delete name=examine_scroll_r suppress
delete name=examine_scroll_l suppress
delete name=examine_scroll suppress
delete name=examine_scroll_none suppress
delete name=companionbutton suppress
delete name=moveoption1 suppress
delete name=moveoption2 suppress
delete name=moveoption3 suppress
delete name=moveoption4 suppress
delete name=moveoption1pic suppress
delete name=moveoption2pic suppress
delete name=moveoption3pic suppress
delete name=moveoption4pic suppress
delete name=back suppress
delete name=_presentbut_ suppress
endmacro

