# Inline evidence macro that doesn't have a pause.
# Notes: Sound requires a filepath if not empty.
# Usage: addinlev <evidence> (<location> [soundpath])|(<customname> <x> <y> [soundpath])
# Example: addinlev gun tl sfx-gunshot || addinlev mask mask 71 30 general/Dunnn.ogg
macro addinlev
setvar ev_key $1
setvar ev_name $2

# Presets, play anim
is_ex ev_name == 'left' OR ev_name == 'tl'?
goto left
is_ex ev_name == 'right' OR ev_name == 'tr'?
goto right

# Custom
# Missing values default to top left.
setvar ev_x $3
isempty ev_x?
setvar ev_x 13
setvar ev_y $4
isempty ev_y?
setvar ev_y 13

setvar ev_sound $5
isempty ev_sound?
setvar ev_sound evidence/ev.ogg
goto display

label left
setvar ev_x 13
setvar ev_y 13
setvar ev_anim general/evidence/showevl
goto displayanim

label right
setvar ev_x 173
setvar ev_y 13
setvar ev_anim general/evidence/showevr
goto displayanim

label displayanim
setvar ev_sound $3
isempty ev_sound?
setvar ev_sound evidence/ev.ogg
sfx $ev_sound
obj $ev_anim name=fg
timer 13 addinlevhelper
goto end

label display
sfx $ev_sound
ev $ev_key x=$ev_x y=$ev_y name=$ev_name

label end
endmacro

# Should only be called through addinlev. This can break if you call addinlev twice within 13 frames so uhh... don't do that.
macro addinlevhelper
ev $ev_key x=$ev_x y=$ev_y name=$ev_name
delete name=fg
endmacro

# Deletes a corresponding addinev. The location should match the exact name used originally (left and tl won't work with eachother).
# Notes: delete <name> should preferably be used in the code.
# Usage: delinlev <location|name> [sound]
# Example: delinlev left [sound]
macro delinlev

setvar delev_name $1
setvar delev_sound $2
isempty delev_sound?
setvar delev_sound evidence/ev.ogg

# Delete
sfx $delev_sound
delete name=$delev_name

# Presets, play anim
is_ex delev_name == 'left' OR delev_name == 'tl'?
goto delleft
is_ex delev_name == 'right' OR delev_name == 'tr'?
goto delright

# No anim
goto end

label delleft
obj general/evidence/hideevl x=0 y=0 name=fg
timer 13 delinlevhelper
goto end

label delright
obj general/evidence/hideevr x=0 y=0 name=fg
timer 13 delinlevhelper
goto end


label end
endmacro

macro delinlevhelper
delete name=fg
endmacro

